plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven'
}

group = 'com.focamacho'
version = '1.4.1'
archivesBaseName = 'SealConfig'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    //Jankson
    implementation 'com.github.falkreon:Jankson:b520ee8e55'

    //Other
    implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    compileOnly group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2"
}

tasks.build.dependsOn(shadowJar)

//Relocate packages to avoid conflicts
shadowJar {
    relocate 'org.apache.commons.text', 'com.focamacho.sealconfig.relocated.org.apache.commons.text'
    relocate 'blue.endless.jankson', 'com.focamacho.sealconfig.relocated.blue.endless.jankson'
    relocate 'org.yaml.snakeyaml', 'com.focamacho.sealconfig.relocated.org.yaml.snakeyaml'

    minimize()
}

tasks.shadowJar.configure {
    classifier = null
}

tasks.withType(JavaCompile) {
    options.encoding('UTF-8')
}

javadoc {
    exclude "blue/endless/jankson/*"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

shadowJar {
    getArchiveFileName().set("${archiveBaseName.get()}-${archiveVersion.get()}.${archiveExtension.get()}")
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}