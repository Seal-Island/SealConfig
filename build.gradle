plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

group = 'com.focamacho'
version = '1.3.0'
archivesBaseName = 'SealConfig'

repositories {
    mavenCentral()
}

dependencies {
    //Jankson
    implementation files('./libs/Jankson-2.0.0.jar')

    //Other
    implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    compileOnly group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2"
}

tasks.build.dependsOn(shadowJar)

//Relocate packages to avoid conflicts
shadowJar {
    relocate 'org.apache.commons.text', 'com.focamacho.sealconfig.relocated.org.apache.commons.text'
    relocate 'blue.endless.jankson', 'com.focamacho.sealconfig.relocated.blue.endless.jankson'
    relocate 'org.yaml.snakeyaml', 'com.focamacho.sealconfig.relocated.org.yaml.snakeyaml'
}

tasks.shadowJar.configure {
    classifier = null
}

tasks.withType(JavaCompile) {
    options.encoding('UTF-8')
}

jar.enabled = false

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Seal-Island/Seal-Config")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}